%%
%% Erlang Redis client
%%
%% Usage:
%%   {ok, Client} = eredis:start_link().
%%   {ok, <<"OK">>} = eredis:q(["SET", "foo", "bar"]).
%%   {ok, <<"bar">>} = eredis:q(["GET", "foo"]).

-module(eredis).
-author('knut.nesheim@wooga.com').

-include("eredis.hrl").

%% Default timeout for calls to the client gen_server
%% Specified in http://www.erlang.org/doc/man/gen_server.html#call-3
-define(TIMEOUT, 5000).

-export([start_link/0, start_link/1, start_link/2, start_link/3, start_link/4,
         start_link/5, stop/1, q/2, q/3, qp/2, qp/3]).

%% Exported for testing
-export([create_multibulk/1]).

%%
%% PUBLIC API
%%

start_link() ->
    case parse_redistogo_uri() of
        {ok, [Host, Port, Password]} ->
            start_link(Host, Port, 0, Password);
        {not_defined, _} ->
            start_link("127.0.0.1", 6379, 0, "")
    end.

start_link(Host, Port) ->
    start_link(Host, Port, 0, "").

start_link(Host, Port, Database) ->
    start_link(Host, Port, Database, "").

start_link(Host, Port,  Database, Password) ->
    start_link(Host, Port, Database, Password, 100).

start_link(Host, Port, Database, Password, ReconnectSleep)
  when is_list(Host);
       is_integer(Port);
       is_integer(Database);
       is_list(Password);
       is_integer(ReconnectSleep) ->

    eredis_client:start_link(Host, Port, Database, Password, ReconnectSleep).


%% @doc: Callback for starting from poolboy
-spec start_link(server_args()) -> {ok, Pid::pid()} | {error, Reason::term()}.
start_link(Args) ->
    [DefHost, DefPort, DefPass] = case parse_redistogo_uri() of
                                      {ok, ConnectInfo} -> 
                                          ConnectInfo;
                                      {not_defined, _} -> 
                                          ["127.0.0.1", 6379, ""]
                                  end,

    Host           = proplists:get_value(host, Args, DefHost),
    Port           = proplists:get_value(port, Args, DefPort),
    Database       = proplists:get_value(database, Args, 0),
    Password       = proplists:get_value(password, Args, DefPass),
    ReconnectSleep = proplists:get_value(reconnect_sleep, Args, 100),
    start_link(Host, Port, Database, Password, ReconnectSleep).

stop(Client) ->
    eredis_client:stop(Client).

-spec q(Client::pid(), Command::iolist()) ->
               {ok, return_value()} | {error, Reason::binary() | no_connection}.
%% @doc: Executes the given command in the specified connection. The
%% command must be a valid Redis command and may contain arbitrary
%% data which will be converted to binaries. The returned values will
%% always be binaries.
q(Client, Command) ->
    call(Client, Command, ?TIMEOUT).

q(Client, Command, Timeout) ->
    call(Client, Command, Timeout).


-spec qp(Client::pid(), Pipeline::pipeline()) ->
                [{ok, return_value()} | {error, Reason::binary()}] |
                {error, no_connection}.
%% @doc: Executes the given pipeline (list of commands) in the
%% specified connection. The commands must be valid Redis commands and
%% may contain arbitrary data which will be converted to binaries. The
%% values returned by each command in the pipeline are returned in a list.
qp(Client, Pipeline) ->
    pipeline(Client, Pipeline, ?TIMEOUT).

qp(Client, Pipeline, Timeout) ->
    pipeline(Client, Pipeline, Timeout).


%%
%% INTERNAL HELPERS
%%

call(Client, Command, Timeout) ->
    Request = {request, create_multibulk(Command)},
    gen_server:call(Client, Request, Timeout).

pipeline(_Client, [], _Timeout) ->
    [];
pipeline(Client, Pipeline, Timeout) ->
    Request = {pipeline, [create_multibulk(Command) || Command <- Pipeline]},
    gen_server:call(Client, Request, Timeout).

-spec create_multibulk(Args::iolist()) -> Command::iolist().
%% @doc: Creates a multibulk command with all the correct size headers
create_multibulk(Args) ->
    ArgCount = [<<$*>>, integer_to_list(length(Args)), <<?NL>>],
    ArgsBin = lists:map(fun to_bulk/1, lists:map(fun to_binary/1, Args)),

    [ArgCount, ArgsBin].

to_bulk(B) when is_binary(B) ->
    [<<$$>>, integer_to_list(iolist_size(B)), <<?NL>>, B, <<?NL>>].

%% @doc: Convert given value to binary. Fallbacks to
%% term_to_binary/1. For floats, throws {cannot_store_floats, Float}
%% as we do not want floats to be stored in Redis. Your future self
%% will thank you for this.
to_binary(X) when is_list(X)    -> list_to_binary(X);
to_binary(X) when is_atom(X)    -> list_to_binary(atom_to_list(X));
to_binary(X) when is_binary(X)  -> X;
to_binary(X) when is_integer(X) -> list_to_binary(integer_to_list(X));
to_binary(X) when is_float(X)   -> throw({cannot_store_floats, X});
to_binary(X)                    -> term_to_binary(X).

%% @doc: Get connection info from REDISTOGO_URL 
parse_redistogo_uri() ->
    URI = os:getenv("REDISTOGO_URL"),
    case string:tokens(URI, ":/*@") of
        ["redis", _UserName, Password, Host, Port] ->
            {ok, [Host, list_to_integer(Port), Password]};
        _->
            {not_defined, []}
    end.
